options {
	LOOKAHEAD = 1;
}

PARSER_BEGIN(YalToJvm)

import java.io.FileNotFoundException;
import java.io.FileInputStream;
import java.io.File;

public class YalToJvm {

	private static Module module;

	public static void main(String[] args) throws ParseException, FileNotFoundException {
		File file = new File(args[0]);
		FileInputStream fs = new FileInputStream(file);
		YalToJvm parser = new YalToJvm(fs);
		SimpleNode root = parser.Module();
		root.dump("");
	}
}

PARSER_END(YalToJvm)

<DEFAULT> SKIP: {
	" "
	| "\t"
	| "\n"
	| "\r"
	| <"//" (~["\n","\r"])* ("\n" | "\r" | "\r\n")>
	| <"/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/">
}

<DEFAULT> TOKEN: {
	<RELA_OP: ">" | "<" | "<=" | ">=" | "==" | "!=">
	| <ADDSUB_OP: "+" | "-">
	| <ARITH_OP: "*" | "/" | "<<" | ">>" | ">>>">
	| <BITWISE_OP: "&" | "|" | "^">
	| <NOT_OP: "!">
	| <WHILE: "while">
	| <IF: "if">
	| <ELSE: "else">
	| <ASSIGN: "=">
	| <ASPA: "\"">
	| <LPAR: "(">
	| <RPAR: ")">
	| <VIRG: ",">
	| <PVIRG: ";">
	| <LCHAVETA: "{">
	| <RCHAVETA: "}">
	| <FUNCTION: "function">
	| <MODULE: "module">
	| <SIZE: "size">
}

<DEFAULT> TOKEN: {
	<INTEGER: (<DIGIT>)+>
	| <ID: <LETTER> (<LETTER> | <DIGIT>)*>
	| <#LETTER: ["$","A"-"Z","_","a"-"z"]>
	| <#DIGIT: ["0"-"9"]>
	| <STRING: <ASPA> (["a"-"z","A"-"Z","0"-"9",":"," ","="])+ <ASPA>>
}

SimpleNode Module() #Module: {Token moduleID;}
{
	<MODULE> moduleID=<ID> {
		jjtThis.ID = moduleID.image;
		module = new Module(moduleID.image);
	} <LCHAVETA> (Declaration())* (Function())* <RCHAVETA> { return jjtThis; }
}

void Declaration() #void: {}
{
	LOOKAHEAD(2) Assign() | LOOKAHEAD(2) (ArrayElement() <PVIRG>) | (ScalarElement() <PVIRG>)
}

void Function() #Function: {Token functionID;}
{
	(LOOKAHEAD(3) (<FUNCTION> (LOOKAHEAD(2) ArrayElement() | ScalarElement()) <ASSIGN> functionID=<ID> {
		jjtThis.ID = functionID.image;
		Function function = new Function(functionID.image);
		module.addFunction(function);
	} <LPAR> (Varlist())? <RPAR>) | (<FUNCTION> functionID=<ID> {
		jjtThis.ID = functionID.image;
		Function function = new Function(functionID.image);
		module.addFunction(function);
	} <LPAR> (Varlist())? <RPAR>)) <LCHAVETA> FunctionBody() <RCHAVETA>
}

void Varlist() #Params: {}
{
	(LOOKAHEAD(2) ArrayElement() | ScalarElement()) (<VIRG> (LOOKAHEAD(2) ArrayElement() | ScalarElement()))*
}

void ArrayElement() #Var: {Token arrayElem;}
{
	arrayElem=<ID> { jjtThis.ID = arrayElem.image; } "[" "]"
}

void ScalarElement() #Var: {Token scalarElem;}
{
	scalarElem=<ID> { jjtThis.ID = scalarElem.image; }
}

void FunctionBody(): {}
{
	(Stmt())*
}

void WhileBody(): {}
{
	(Stmt())*
}

void IfBody(): {}
{
	(Stmt())*
}

void ElseBody(): {}
{
	(Stmt())*
}

void Stmt() #void: {}
{
	While() | If() | LOOKAHEAD(3) Assign() | Call() <PVIRG>
}

void Assign() #Assignement: {}
{
	Lhs() <ASSIGN> Rhs() <PVIRG>
}

void Lhs() #void: {}
{
	LOOKAHEAD(2) ArrayAccess() | ScalarAccess()
}

void Rhs(): {Token op;}
{
	(Term() ((op=<ARITH_OP> {
		jjtThis.Op = op.image;
	} | op=<BITWISE_OP> {
		jjtThis.Op = op.image;
	} | op=<ADDSUB_OP> {
		jjtThis.Op = op.image;
	}) Term())?) | "[" ArraySize() "]"
}

void ArraySize(): {}
{
	ScalarAccess() | <INTEGER>
}

void Term(): {Token op, intID;}
{
	(op=<ADDSUB_OP> {
		jjtThis.Op = op.image;
	})? (intID=<INTEGER> {
		jjtThis.ID = intID.image;
	} | LOOKAHEAD(3) Call() | LOOKAHEAD(2) ArrayAccess() | ScalarAccess())
}

void Exprtest() #Condition: {Token relaOpID;}
{
	<LPAR> Lhs() relaOpID=<RELA_OP> { jjtThis.ID = relaOpID.image; } Rhs() <RPAR>
}

void While(): {}
{
	<WHILE> Exprtest() <LCHAVETA> WhileBody() <RCHAVETA>
}

void If(): {}
{
	<IF> Exprtest() <LCHAVETA> IfBody() <RCHAVETA> (<ELSE> <LCHAVETA> ElseBody() <RCHAVETA>)?
}

void Call(): {}
{
	<ID> ("." <ID>)? <LPAR> (ArgumentList())? <RPAR>
}

void ArgumentList() #Arguments: {}
{
	Argument() (<VIRG> Argument())*
}

void Argument(): {}
{
	<ID> | <STRING> | <INTEGER>
}

void ArrayAccess(): {Token arrayID;}
{
	arrayID=<ID> { jjtThis.ID = arrayID.image; } "[" Index() "]"
}

void ScalarAccess(): {Token scalarID;}
{
	scalarID=<ID> { jjtThis.ID = scalarID.image; } ("." <SIZE>)?
}

void Index(): {}
{
	<ID> | <INTEGER>
}
